var CSSParser = require('../css/parser').CSSParser;

ometa BOSSParser <: CSSParser {

  statement = definition | ruleset | atrule | assignment,

  stylesheet = (cdo | cdc | statement)*:x -> CSSParser.concat([#stylesheet], x),

  definition = sc* ident:x '(' functionBody:y ')' sc* block:z -> [#definition, x, y, z],

  string = m_string:x -> [#string, x[0], x.slice(1, -1)],

  variable = '$' ident:x -> [#variable, x],

  combinator = ('+' | '>' | '~' | '&'):x -> [#combinator, x],

  property = (ident | variable):x sc*:s0 -> CSSParser.concat([#property, x], s0),

  assignment = property:p '=' value:v decldelim? -> [#assignment, p, v],

  primary = percentage | dimension | number,

  unary = ('-' | '+'):op primary:x -> [#unary, op, x]
        | primary,

  multiplicative = multiplicative:x s+ ('*' | '/' | '%'):op s+ unary:y -> [#binary, [#operator, op], x, y]
                 | unary,

  additive = additive:x s+ ('+' | '-'):op s+ multiplicative:y -> [#binary, [#operator, op], x, y]
           | multiplicative,

  expression = additive,

  nthselector = nthf:x '(' (sc | ('+' | '-'):op -> [#operator, op] | nth)*:y ')' -> [#nthselector, [#ident, x]].concat(y),

  blockdecl = sc* (filter | declaration):x decldelim? sc* -> [x]
            | sc* decldelim:x sc*
            | sc* ruleset:x sc* -> [x]
            | sc+,

  uri = seq('url(') sc* string:x sc* ')' -> CSSParser.concat([#uri], [x])
        | seq('url(') sc* (~')' ~m_w char)*:x sc* ')' -> CSSParser.concat([#uri], [[#raw, x.join('')]]),

  any = braces | string | expression | uri | functionExpression | funktion | ident | variable
}